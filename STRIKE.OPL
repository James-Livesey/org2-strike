STRIKE:
GLOBAL ARENA%(5)
LOCAL TURN%,DICE%
LOCAL P%(2)
LOCAL ROLL%,I%,COUNT%,GAIN%
TURN%=1
P%(1)=9 :P%(2)=9
ARENA%(RND*5+1)=1
LOOP::
IF TURN%=1
 DO
  CLS
  AT 1,2 :STKARENA:
  AT 1,1 :PRINT "DICE (1-";P%(1);"): ";
  INPUT DICE%
  IF DICE%<1 :STOP :ENDIF
 UNTIL DICE%<=P%(1)
ELSE
 CLS
 PRINT "MY TURN"
 DICE%=RND*P%(2)+1
 PAUSE 20
ENDIF
CLS
PRINT "ROLL"
STKARENA:
PAUSE 20 :PRINT "+";
GAIN%=0
WHILE DICE%>0
 ROLL%=RND*6+1
 IF ROLL%=1
  PRINT "X";
 ELSE
  PRINT ROLL%;
  ARENA%(ROLL%-1)=ARENA%(ROLL%-1)+1
 ENDIF
 PAUSE 20
 GAIN%=GAIN%-1
 DICE%=DICE%-1
ENDWH
CLS
PRINT "END OF TURN"
STKARENA:
PAUSE 40
I%=1
WHILE I%<=5
 COUNT%=ARENA%(I%)
 IF COUNT%>1
  GAIN%=GAIN%+COUNT%
  ARENA%(I%)=0
 ENDIF
 I%=I%+1
ENDWH
CLS
PRINT "FINAL"
STKARENA:
PAUSE 20
P%(TURN%)=P%(TURN%)+GAIN%
IF P%(1)=0
 CLS :PRINT "COMPUTER WINS"
ELSEIF P%(2)=0
 CLS :PRINT "HUMAN WINS"
ENDIF
IF P%(TURN%)=0
 GET :STOP
ENDIF
TURN%=3-TURN%
CLS
PRINT "HUMAN    ";P%(1)
PRINT "COMPUTER ";P%(2)
PAUSE 40
GOTO LOOP::
